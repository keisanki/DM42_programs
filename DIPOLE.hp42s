@ Polarizabilities used:
@ Er: 166 a.u. at 1064.5 nm from Tab. I of
@     J. H. Becher et al., Phys. Rev. A 97, 012509 (2018).
@ Yb: 162.5 a.u. at 1064 nm from Tab. B.2 of
@     C. D. Herold, Dissertation, University of Maryland, 2014.
@ Li: 269 a.u. by interpolation to 1070 nm of data in Tab. I of
@     M. S. Safronova et al., Phys. Rev. A 73, 022505 (2006).
@
@ Note that the calculation of Herold (PhD) for Rb is in close agreement with
@ the results given by Safronoa (PRA), giving credibility to Herold's data.

LBL "MROT"
@ Creates 3x3 rotation matrix Mrot = Mrot_z * Mrot_y * Mrot_x, where Mrot_i
@ is a rotation about axis i.
@
@ input: 
@  ST X, ST Y, ST Z: rotation angles along X, Y, Z
@ output: 
@  MROT: rotation matrix
@  ST X: rotation matrix (copy of MROT)
@
@ interally used:
@  REG 01: initial ST X (rotation angle about x)
@  REG 02: initial ST Y (rotation angle about y)
@  REG 03: initial ST Z (rotation angle about z)


LBL "WSTZ"
@ Calculates beam radius of Gaussian beam at distance Z from waist position.
@
@ input:
@  ST x: distance Z from waist
@  LAMBDA: wavelength
@  WAIST: waist size
@
@ output:
@  ST X: beam radius at Z


LBL "IGAUSS"
@ Calculates the intensity of an elliptical Gauss beam oriented in an 
@ arbitrary direction at and arbitrary position in space. The beam is
@ assumed to go through the origin of the coordinate system and to also
@ have its waist there.
@
@ input:
@  ME: 3x3 matrix containing unit vectors with beam geometry.
@      column 1: direction of 1st waist
@      column 2: direction of 2nd waist
@      column 3: direction of beam propagation
@      (Effectively this is just MROT)
@  WST1: size of 1st waist
@  WST2: size of 2nd waist
@  LAMBDA: wavelength
@  VR: 3x1 vector with (x, y, z) laboratory position where intensity is wanted
@  POWER: Total power of beam
@
@ output:
@  ST X: intensity at position VR
@
@ internally used:
@  REG 04: position in 1st waist direction
@  REG 05: position in 2nd waist direction
@  REG 06: position along beam axis
@  REG 07: radius of beam in 1st waist direction
@  REG 08: radius of beam in 2nd waist direction
@  REG 10-12: used during plotting
	LBL A
	@ Get (rotated) unit vector that defines beam coordinate system
	@ input: 
	@  ST X: axis selection (1, 2 or 3)
	@  ME: 3x3 matrix containing unit vectors as column vectors
	@ return: 
	@  ST X: 3x1 vector that is the column vector of MROT at position ST X
	LBL "B"
	@ Get position of laboratory frame vector for one axis in the beam frame
	@ input:
	@  ST X: beam frame axis selection (1, 2 or 3)
	@  VR: 3x1 column vector with (x, y, z) laboratory frame position
	@  ME: 3x3 matrix containing unit vectors as column vectors
	@ output:
	@  ST X: position of vector along axis that was selected by ST X

LBL "IFORT"
@ Calculates the intensity of the crossed FORT at the given position and
@ with the given powers.
@
@ For the ErYbLi experiment we assume the following laboratory coordinate frame:
@  X: Zeeman slower direction
@  Y: H-FORT direction
@  Z: V-FORT direction
@
@ input:
@  VR: 3x1 vector with (x, y, z) laboratory position where intensity is wanted
@  PWRH: Power of horizontal FORT
@  PWRV: Power of vertical FORT
@  MEH: Maxtrix of H-FORT unit column vectors
@       (waist 1 direction, waist 2 direction, propagation direction)
@  MEV: Maxtrix of V-FORT unit colums vectors
@       (waist 1 direction, waist 2 direction, propagation direction)
@
@ output:
@  ST X: intensity at position VR
@
@ internally used:
@  REG 04-08: in the execution of IGAUSS
@  REG 09: intermediate result from intensity calculation
@  LAMBDA: wavelength of beams
@  POWER: power of current FORT beam
@  WST1, WST2: waists of beams

LBL "UFORT"
@ Calculates the potential of the crossed FORT including gravity.
@ Currently polarization and mass are hard-coded for Erbium.
@
@ input:
@  VR: 3x1 vector with (x, y, z) laboratory position where potential is wanted
@  POLARI: Polarizability in atomic units (positive for attractive potential)
@  MASS: Atomic mass in amu
@  PWRH: Power of horizontal FORT (used by IFORT)
@  PWRV: Power of vertical FORT (used by IFORT)
@  MEH: Matrix of H-FORT unit column vectors
@       (waist 1 direction, waist 2 direction, propagation direction)
@  MEV: Matrix of V-FORT unit column vectors
@       (waist 1 direction, waist 2 direction, propagation direction)
@
@ output:
@  ST X: potential at VF (in Joule)
@
@ internally used:
@  REG 04-08: in the execution of IGAUSS
@  REG 09: intermediate result from intensity calculation
@  LAMBDA, WST1, WST2: in the execution of IFORT

LBL "UDERIV"
@ Calculates either the 1st derivatives (Jacobian) of the potential (FLAG 01 is 
@ false) or the 2nd derivates (Hessian) (FLAG 01 is true) at position VR.
@ 
@ input:
@  VR: 3x1 vector with (x, y, z) laboratory position where potential is wanted
@  PWRH: Power of horizontal FORT (used by IFORT)
@  PWRV: Power of vertical FORT (used by IFORT)
@  MEH: Matrix of H-FORT unit column vectors
@       (waist 1 direction, waist 2 direction, propagation direction)
@  MEV: Matrix of V-FORT unit colums vectors
@       (waist 1 direction, waist 2 direction, propagation direction)
@  FLAG 01: 1st or 2nd derivative selection
@
@ output:
@  ST X: derivative (either 3x1 or 3x3 matrix)
@
@ internally used (global storage):
@  REG 04-08: in the execution of IGAUSS
@  REG 09: in the execution of IFORT
@  REG 10: step size in numerical derivative
@  LAMBDA, WST1, WST2: in the execution of IFORT
@ internally used (local storage):
@  COOR: coordinate for which to calculate the derivative (1, 2 or 3)
@  VDER1: 3x1 column vector that holds the 1st derivative
	LBL A
	@ Calculate a single 1st derivative and store result into the 
	@ correct position of VDER1.
	@
	@ input:
	@  COOR: coordinate for which to calculate the derivative (1, 2 or 3)
	@
	@ output:
	@  VDER1: 3x1 column vector of 1st derivatives with updated element
	@
	@ internally used (local storage):
	@  VAL1: function value at positive step
	@  VAL2: function value at negative step
	LBL B
	@ Calculate the complete Hessian matrix.
	@
	@ input:
	@  -- nothing additional --
	@
	@ output:
	@  ST X: 2nd derivative 3x3 matrix
	@
	@ internally used (local storage):
	@  MDER2: 3x3 matrix that hold the 2nd derivative
	@  C2: coordinate of the 2nd derivative
	LBL C
	@ Calculate a single element of the Hessian matrix and store
	@ the value into the correct position of MDER2.
	@
	@ input:
	@  COOR: coordinate of the 'inner' 1st derivate
	@  ST X: coordinate of the 2nd derivative
	@
	@ output:
	@  MDER2: 3x3 matrix of 2nd derivatives with updated element
	@
	@ internally used (local storage):
	@  VAL1: function value at positive step
	@  VAL2: function value at negative step

LBL "FORT"
@ Finds the minimum of the FORT potential and optionally calculates the
@ trap frequencies.
@
@
@ input:
@  MEH: Matrix of H-FORT unit column vectors 
@       (waist 1 direction, waist 2 direction, propagation direction)
@  MEV: Matrix of V-FORT unit colums vectors 
@       (waist 1 direction, waist 2 direction, propagation direction)
@
@ output:
@  TRAPF: 3x1 column vector with trap frequencies (Hz)
@  UMIN: Potential at trap minimum (in µK)
@
@ internally used (global storage):
@  PWRH: Power of horizontal FORT (used by IFORT)
@  PWRV: Power of vertical FORT (used by IFORT)
@  MASS: atomic mass in amu
@  VR: 3x1 column vector of position
@  VRMIN: 3x1 column vector of trap minimum position
@  VRMAX: 3x1 column vector of trap minimum position
@  H: 3x3 Hessian matrix at trap minimum
@  ATOM: name of species (e.g. 168Er)
@  FLAG 01: Calculate Jacobian or Hessian
@  FLAG 02: Search for maximum instead of minimum
@  FLAG 03: Error in searching extremum (no trap?)
@  FLAG 04: Recalculation of trap geometry needed if set

	LBL A
	LBL a
	@ Program menus
	@
	LBL 20
	@ Dummy loop for program menu
	LBL 11
	@ H-FORT power setting
	LBL 12
	@ V-FORT power setting
	
	LBL B
	@ Search for trap minimum and trap maximum.
	@ Calls LBL C two times, without and with FLAG 02 set.
	@ Also calculates final Hessian and potentials,
	@ and save all calculation results
	@
	@ output:
	@  H: 3x3 Hessian matrix at trap minimum
	@  UMIN: Potential at trap minimum (in µK)
	@  UMAX: Potential at trap maximum (in µK)
	@  VRMIN: 3x1 column vector of trap minimum position
	@  VRMAX: 3x1 column vector of trap maximum position

	LBL C
	@ Newton Algorithm.
	@ Determines either minimum (FLAG 02 false) or maximum (FLAG 02 true).
	@
	@  N: counter for number of Newton iterations
	LBL 02
	@ Loop for initial minimum/maximum search.
	@ If FLAG 02 is false, searches for minimum.
	@ If FLAG 02 is true, searches for maximum.
	@ Starts at current position VR and goes down in steps of 2 µm
	@ until beyond minimum or maximum.
	@
	@ internally used (local storage):
	@  VAL: current potential value
	@  M: counter of search interations (30 iterations max)
	LBL 01
	@ Newton Algorithm main loop
	@
	@ internally used (local storage):
	@  J: Jacobian 3x1 column vector
	@  DELTA: 3x1 column vector, current Newton step size
	LBL 03
	@ Could not find trap extremum. Cancel Newton.
	@
	@ output:
	@  FLAG 03: set to true
	LBL 07
	@ Newton algorithm converged
	LBL 08
	@ Newton algorithm did not converge within N steps
	LBL 06
	@ Display trap depth and gravitational sag
	@
	@ input:
	@  UMIN: Potential at trap minimum (in µK)
	@  UMAX: Potential at trap maximum (in µK)
	@  VRMIN: 3x1 column vector of trap minimum position
	@
	@ output:
	@  ALPHA: informational message

	LBL D
	@ Calculates the eigenvectors of a 3x3 matrix.
	@ Follows the implementation of 
	@ https://en.wikipedia.org/wiki/Eigenvalue_algorithm
	@
	@ input:
	@  ST X: 3x3 matrix (in this case the Hessian matrix)
	@  MASS: atomic mass in amu
	@
	@ output:
	@  TRAPF: 3x1 column vector with trap frequencies (Hz)
	@
	@ internally used (local storage):
	@  A: copy of 3x3 matrix for which eigenvalues are computed
	@  Q: trace(H)/3
	@  P1, P2, P, PHI: intermediate values
	@  B: 3x3 intermediate matrix
	@  EIG1, EIG2, EIG3: eigenvalues
	
	LBL E
	@ Plot trap potential in vertical direction.
	@ If FLAG 04 is set plots along the line connecting trap
	@ minimum and maximum, otherwise through zero along Z.
	@
	@ internally used (local storage):
	@  VDIR: 3x1 unit column vector of plot direction
	LBL 30
	@ Plot direction is determined, start plot
	@
	@ input:
	@  VDIR: 3x1 unit column vector of plot direction
	@
	@ internally used:
	@  VR: 3x1 vector of current plot position
	LBL 31
	@ main plotting loop
	LBL 32
	@ Determine vertical position of pixel from ST X function value
	LBL 33
	@ Draw a vertical line
	@
	@ input:
	@  ST X: position (in spatial units)
	LBL 34
	@ Draw a horizontal line
	@
	@ input:
	@  ST X: position (in µK units)
	LBL c
	@ Calculate potential at given position
	@ The position will be translated into a 3D point along
	@ the plot axis.
	@
	@ input:
	@  ST X: position (in m)
	@
	@ output:
	@  ST X: potential in µK
	@
	@ internally used (global storage):
	@  XMIN, XMAX: minimal and maximal vertical value
	@  YMIN, YMAX: minimal and maximal vertical value
	
	LBL 41/42/43
	@ Species selection for Er/Yb/Li
	@
	@ output:
	@  POLARI: polarizability in atomic units (positive for attractive pot.)
	@  MASS: atomic mass in amu
	
	LBL 51/52
	@ Summary printout to HP82240A printer
	
	LBL 99
	@ Exits the program cleanly.
	@ Clears FLAG 04 and the programmable menu.
	
2021/02/01 program listing:
===========================

(2021/04/25 printout additions added)

00 { 156-Byte Prgm }
01▸LBL "MROT"
02 STO 01
03 R↓
04 STO 02
05 R↓
06 STO 03
07 3
08 3
09 NEWMAT
10 STO "MROT"
11 INDEX "MROT"
12 1
13 1
14 STOIJ
15 RCL 03
16 COS
17 RCL 02
18 COS
19 ×
20 STOEL
21 J+
22 RCL 02
23 SIN
24 RCL 01
25 SIN
26 RCL 03
27 COS
28 ×
29 ×
30 RCL 03
31 SIN
32 RCL 01
33 COS
34 ×
35 -
36 STOEL
37 J+
38 RCL 02
39 SIN
40 RCL 03
41 COS
42 RCL 01
43 COS
44 ×
45 ×
46 RCL 03
47 SIN
48 RCL 01
49 SIN
50 ×
51 +
52 STOEL
53 J+
54 RCL 03
55 SIN
56 RCL 02
57 COS
58 ×
59 STOEL
60 J+
61 RCL 03
62 COS
63 RCL 01
64 COS
65 ×
66 RCL 03
67 SIN
68 RCL 02
69 SIN
70 RCL 01
71 SIN
72 ×
73 ×
74 +
75 STOEL
76 J+
77 RCL 03
78 SIN
79 RCL 02
80 SIN
81 RCL 01
82 COS
83 ×
84 ×
85 RCL 01
86 SIN
87 RCL 03
88 COS
89 ×
90 -
91 STOEL
92 J+
93 RCL 02
94 SIN
95 +/-
96 STOEL
97 J+
98 RCL 01
99 SIN
100 RCL 02
101 COS
102 ×
103 STOEL
104 J+
105 RCL 02
106 COS
107 RCL 01
108 COS
109 ×
110 STOEL
111 RCL "MROT"
112 END

00 { 39-Byte Prgm }
01▸LBL "WSTZ"
02 RCL "WAIST"
03 X^2
04 PI
05 ×
06 RCL÷ "LAMBDA"
07 ÷
08 X^2
09 1
10 +
11 SQRT
12 RCL× "WAIST"
13 END

00 { 142-Byte Prgm }
01▸LBL "IGAUSS"
02 1
03 XEQ B
04 STO 04
05 2
06 XEQ B
07 STO 05
08 3
09 XEQ B
10 STO 06
11 RCL "W1"
12 STO "WAIST"
13 RCL 06
14 XEQ "WSTZ"
15 STO 07
16 RCL "W2"
17 STO "WAIST"
18 RCL 06
19 XEQ "WSTZ"
20 STO 08
21 RCL 05
22 X^2
23 -2
24 ×
25 RCL 08
26 X^2
27 ÷
28 E^X
29 RCL 04
30 X^2
31 -2
32 ×
33 RCL 07
34 X^2
35 ÷
36 E^X
37 ×
38 RCL× "POWER"
39 2
40 ×
41 PI
42 ÷
43 RCL÷ 07
44 RCL÷ 08
45 GTO 99
46▸LBL A
	47 INDEX "ME"
	48 1
	49 X<>Y
	50 STOIJ
	51 3
	52 1
	53 GETM
	54 RTN
55▸LBL B
	56 XEQ A
	57 TRANS
	58 RCL× "VR"
	59 DET
	60 RTN
61▸LBL 99
	62 END

00 { 154-Byte Prgm }
01▸LBL "IFORT"
02 RCL "MEH"
03 STO "ME"
04 1ᴇ-4
05 STO "W1"
06 2ᴇ-5
07 STO "W2"
08 107ᴇ-8
09 STO "LAMBDA"
10 RCL "PWRH"
11 STO "POWER"
12 XEQ "IGAUSS"
13 STO 09
14 RCL "MEV"
15 STO "ME"
16 1ᴇ-4
17 STO "W1"
18 STO "W2"
19 1083ᴇ-9
20 STO "LAMBDA"
21 RCL "PWRV"
22 STO "POWER"
23 XEQ "IGAUSS"
24 RCL+ 09
25 CLV "W1"
26 CLV "W2"
27 CLV "ME"
28 CLV "POWER"
29 END

00 { 73-Byte Prgm }
01▸LBL "UFORT"
02 XEQ "IFORT"
03 RCL× "POLARI"
04 -3.10572189868ᴇ-39
05 ×
06 INDEX "VR"
07 3
08 1
09 STOIJ
10 RCLEL
11 X<>Y
12 R↓
13 X<>Y
14 R↓
15 9.80665
16 ×
17 166053886ᴇ-35
18 ×
19 RCL "MASS"
20 ×
21 +
22 END

00 { 484-Byte Prgm }
01▸LBL "UDERIV"
02 3
03 1
04 NEWMAT
05 LSTO "VDER1"
06 5ᴇ-8
07 STO 10
08 1
09 LSTO "COOR"
10 FS? 01
11 GTO B
12 XEQ A
13 2
14 LSTO "COOR"
15 XEQ A
16 3
17 LSTO "COOR"
18 XEQ A
19 RCL "VDER1"
20 GTO 99
21▸LBL A
	22 INDEX "VR"
	23 RCL "COOR"
	24 1
	25 STOIJ
	26 RCLEL
	27 RCL+ 10
	28 STOEL
	29 XEQ "UFORT"
	30 LSTO "VAL1"
	31 INDEX "VR"
	32 RCL "COOR"
	33 1
	34 STOIJ
	35 RCLEL
	36 RCL- 10
	37 RCL- 10
	38 STOEL
	39 XEQ "UFORT"
	40 LSTO "VAL2"
	41 INDEX "VR"
	42 RCL "COOR"
	43 1
	44 STOIJ
	45 RCLEL
	46 RCL+ 10
	47 STOEL
	48 RCL "VAL1"
	49 RCL- "VAL2"
	50 2
	51 ÷
	52 RCL÷ 10
	53 INDEX "VDER1"
	54 RCL "COOR"
	55 1
	56 STOIJ
	57 R↓
	58 R↓
	59 STOEL
	60 RTN
61▸LBL B
	62 3
	63 3
	64 NEWMAT
	65 LSTO "MDER2"
	66 1
	67 LSTO "COOR"
	68 LSTO "C2"
	69 XEQ C
	70 2
	71 LSTO "C2"
	72 XEQ C
	73 3
	74 LSTO "C2"
	75 XEQ C
	76 2
	77 LSTO "COOR"
	78 LSTO "C2"
	79 XEQ C
	80 3
	81 LSTO "C2"
	82 XEQ C
	83 3
	84 LSTO "COOR"
	85 LSTO "C2"
	86 XEQ C
	87 INDEX "MDER2"
	88 1
	89 2
	90 STOIJ
	91 RCLEL
	92 R↓
	93 X<>Y
	94 STOIJ
	95 R^
	96 STOEL
	97 1
	98 3
	99 STOIJ
	100 RCLEL
	101 R↓
	102 X<>Y
	103 STOIJ
	104 R^
	105 STOEL
	106 2
	107 3
	108 STOIJ
	109 RCLEL
	110 R↓
	111 X<>Y
	112 STOIJ
	113 R^
	114 STOEL
	115 RCL "MDER2"
	116 GTO 99
117▸LBL C
	118 INDEX "VR"
	119 RCL "C2"
	120 1
	121 STOIJ
	122 RCLEL
	123 RCL+ 10
	124 STOEL
	125 XEQ A
	126 LSTO "VAL1"
	127 INDEX "VR"
	128 RCL "C2"
	129 1
	130 STOIJ
	131 RCLEL
	132 RCL- 10
	133 RCL- 10
	134 STOEL
	135 XEQ A
	136 LSTO "VAL2"
	137 INDEX "VR"
	138 RCL "C2"
	139 1
	140 STOIJ
	141 RCLEL
	142 RCL+ 10
	143 STOEL
	144 RCL "VAL1"
	145 RCL- "VAL2"
	146 2
	147 ÷
	148 RCL÷ 10
	149 INDEX "MDER2"
	150 RCL "COOR"
	151 RCL "C2"
	152 STOIJ
	153 R↓
	154 R↓
	155 STOEL
	156 RTN
157▸LBL 99
	158 END

00 { 1815-Byte Prgm }
01▸LBL "FORT"
02 SF 04
03 0
04 STO "UMIN"
05 STO "UMAX"
06 CLST
07▸LBL A
	08 CLMENU
	09 "H-VLT"
	10 KEY 1 XEQ 11
	11 "V-VLT"
	12 KEY 2 XEQ 12
	13 "CALC"
	14 KEY 3 XEQ B
	15 "G SAG"
	16 KEY 4 XEQ 06
	17 "FREQ"
	18 KEY 5 XEQ D
	19 "PLOT"
	20 KEY 6 XEQ E
	21 KEY 7 GTO a
	22 KEY 8 GTO a
	23 KEY 9 XEQ 99
	24 MENU
	25 GTO 10
26▸LBL a
	27 CLMENU
	28 "Er"
	29 KEY 1 XEQ 41
	30 "Yb"
	31 KEY 2 XEQ 42
	32 "Li"
	33 KEY 3 XEQ 43
	   "PRNT"
	   KEY 6 XEQ 51
	34 KEY 7 GTO A
	35 KEY 8 GTO A
	36 KEY 9 XEQ 99
	37 MENU
	38 GTO 10
39▸LBL 10
	40 STOP
	41 GTO 10
42▸LBL 11
	43 INPUT "H-VOLT"
	44 RCL "H-VOLT"
	45 3
	46 Y^X
	47 -59599ᴇ-7
	48 ×
	49 RCL "H-VOLT"
	50 X^2
	51 -0.048993
	52 ×
	53 +
	54 RCL "H-VOLT"
	55 9.25894
	56 ×
	57 +
	58 -999039ᴇ-8
	59 +
	60 0.875
	61 ×
	62 "H-FORT at "
	63 ARCL ST X
	64 ├" W"
	65 AVIEW
	66 RCL "PWRH"
	67 X≠Y?
	68 SF 04
	69 R↓
	70 STO "PWRH"
	71 RTN
72▸LBL 12
	73 INPUT "V-VOLT"
	74 RCL "V-VOLT"
	75 3
	76 Y^X
	77 -260963ᴇ-8
	78 ×
	79 RCL "V-VOLT"
	80 X^2
	81 -214502ᴇ-8
	82 ×
	83 +
	84 RCL "V-VOLT"
	85 6.56503
	86 ×
	87 +
	88 -0.0599696
	89 +
	90 0.695313
	91 ×
	92 "V-FORT at "
	93 ARCL ST X
	94 ├" W"
	95 AVIEW
	96 RCL "PWRV"
	97 X≠Y?
	98 SF 04
	99 R↓
	100 STO "PWRV"
	101 RTN
102▸LBL B
	103 3
	104 1
	105 NEWMAT
	106 STO "VR"
	107 CF 03
	108 CF 02
	109 XEQ C
	110 FS?C 03
	111 RTN
	112 RCL "VR"
	113 STO "VRMIN"
	114 SF 01
	115 XEQ "UDERIV"
	116 STO "H"
	117 CF 01
	118 XEQ "UFORT"
	119 1380649ᴇ-29
	120 ÷
	121 1ᴇ6
	122 ×
	123 STO "UMIN"
	124 SF 02
	125 XEQ C
	126 FS?C 03
	127 RTN
	128 RCL "VR"
	129 STO "VRMAX"
	130 CF 02
	131 XEQ "UFORT"
	132 1380649ᴇ-29
	133 ÷
	134 1ᴇ6
	135 ×
	136 STO "UMAX"
	137 CF 04
	138 PSE
	    XEQ D
	139 XEQ 06
	140 RTN
141▸LBL C
	142 6
	143 LSTO "N"
	144 30
	145 LSTO "M"
	146 100
	147 FS? 02
	148 +/-
	149 LSTO "VAL"
	150▸LBL 02
		151 XEQ "UFORT"
		152 RCL "VAL"
		153 FS? 02
		154 X<>Y
		155 X<Y?
		156 GTO 01
		157 FC? 02
		158 R↓
		159 LSTO "VAL"
		160 -1
		161 RCL+ "M"
		162 X=0?
		163 GTO 03
		164 LSTO "M"
		165 INDEX "VR"
		166 3
		167 1
		168 STOIJ
		169 RCLEL
		170 2ᴇ-6
		171 -
		172 STOEL
		173 GTO 02
	174▸LBL 01
		175 RCL "N"
		176 X=0?
		177 GTO 08
		178 1
		179 STO- "N"
		180 CF 01
		181 XEQ "UDERIV"
		182 LSTO "J"
		183 SF 01
		184 XEQ "UDERIV"
		185 CF 01
		186 INVRT
		187 RCL "J"
		188 ×
		189 LSTO "DELTA"
		190 STO- "VR"
		191 FNRM
		192 5ᴇ-8
		193 X>Y?
		194 GTO 07
		195 GTO 01
	196▸LBL 03
		197 "No extremum "
		198 ├"found."
		199 AVIEW
		200 SF 03
		201 RTN
	202▸LBL 07
		203 "Converged after"
		204 ├" "
		205 6
		206 RCL- "N"
		207 AIP
		208 AVIEW
		209 RTN
	210▸LBL 08
		211 "Not converged"
		212 AVIEW
		213 CF 04
		214 RTN
	215▸LBL 06
		216 FS? 04
		217 RTN
		218 "Trap depth: "
		219 RCL "UMAX"
		220 RCL- "UMIN"
		221 ARCL ST X
		222 ├" μK"
		223 ├"[LF]Grav. sag: "
		    ├" "
		224 INDEX "VRMIN"
		225 3
		226 1
		227 STOIJ
		228 RCLEL
		229 1ᴇ6
		230 ×
		231 +/-
		232 ARCL ST X
		233 ├" μm"
		234 AVIEW
		235 RTN
236▸LBL D
	237 FS? 04
	238 RTN
	239 RCL "H"
	240 RAD
	241 LSTO "A"
	242 INDEX "A"
	243 1
	244 2
	245 STOIJ
	246 RCLEL
	247 X^2
	248 J+
	249 RCLEL
	250 X^2
	251 +
	252 I+
	253 RCLEL
	254 X^2
	255 +
	256 LSTO "P1"
	257 1
	258 1
	259 STOIJ
	260 RCLEL
	261 I+
	262 J+
	263 RCLEL
	264 +
	265 I+
	266 J+
	267 RCLEL
	268 +
	269 3
	270 ÷
	271 LSTO "Q"
	272 1
	273 1
	274 STOIJ
	275 RCLEL
	276 RCL- "Q"
	277 X^2
	278 I+
	279 J+
	280 RCLEL
	281 RCL- "Q"
	282 X^2
	283 +
	284 I+
	285 J+
	286 RCLEL
	287 RCL- "Q"
	288 X^2
	289 +
	290 2
	291 RCL× "P1"
	292 +
	293 6
	294 ÷
	295 SQRT
	296 LSTO "P"
	297 3
	298 3
	299 NEWMAT
	300 LSTO "B"
	301 INDEX "B"
	302 1
	303 1
	304 STOIJ
	305 RCL "Q"
	306 STOEL
	307 I+
	308 J+
	309 STOEL
	310 I+
	311 J+
	312 STOEL
	313 RCL "A"
	314 RCL- "B"
	315 RCL÷ "P"
	316 LSTO "B"
	317 DET
	318 2
	319 ÷
	320 ACOS
	321 3
	322 ÷
	323 LSTO "PHI"
	324 COS
	325 RCL× "P"
	326 2
	327 ×
	328 RCL+ "Q"
	329 LSTO "EIG1"
	330 2
	331 PI
	332 ×
	333 3
	334 ÷
	335 RCL+ "PHI"
	336 COS
	337 RCL× "P"
	338 2
	339 ×
	340 RCL+ "Q"
	341 LSTO "EIG3"
	342 3
	343 RCL× "Q"
	344 RCL- "EIG1"
	345 RCL- "EIG3"
	346 LSTO "EIG2"
	347 DEG
	348 3
	349 1
	350 NEWMAT
	351 STO "TRAPF"
	352 INDEX "TRAPF"
	353 1
	354 1
	355 STOIJ
	356 RCL "EIG1"
	357 STOEL
	358 J+
	359 RCL "EIG2"
	360 STOEL
	361 J+
	362 RCL "EIG3"
	363 STOEL
	364 RCL "TRAPF"
	365 RCL "MASS"
	366 166053886ᴇ-35
	367 ×
	368 ÷
	369 SQRT
	370 2
	371 ÷
	372 PI
	373 ÷
	374 STO "TRAPF"
	375 INDEX "TRAPF"
	376 1
	377 1
	378 STOIJ
	379 CLST
	380 RCLEL
	381 J+
	382 RCLEL
	383 J+
	384 RCLEL
	385 "Trap freqs. "
	386 ├"below (Hz)"
	387 AVIEW
	388 RTN
389▸LBL E
	390 3
	391 1
	392 NEWMAT
	393 LSTO "VDIR"
	394 STO "VR"
	395 INDEX "VDIR"
	396 3
	397 1
	398 STOIJ
	399 1
	400 STOEL
	401 FS? 04
	402 GTO 30
	403 RCL "VRMIN"
	404 RCL- "VRMAX"
	405 ENTER
	406 FNRM
	407 ÷
	408 LSTO "VDIR"
	409▸LBL 30
		410 3ᴇ-5
		411 STO "XMAX"
		412 -3ᴇ-5
		413 STO "XMIN"
		414 RCL "XMAX"
		415 XEQ c
		416 STO "YMAX"
		417 RCL "XMIN"
		418 XEQ c
		419 STO "YMIN"
		420 RCL "UMIN"
		421 1
		422 -
		423 X<Y?
		424 STO "YMIN"
		425 RCL "ResY"
		426 1
		427 -
		428 RCL "YMAX"
		429 RCL- "YMIN"
		430 ÷
		431 STO 10
		432 RCL "XMIN"
		433 STO 11
		434 1
		435 RCL "ResX"
		436 1ᴇ3
		437 ÷
		438 +
		439 STO 12
		440 EXITALL
		441 CLLCD
		442 RCL "UMAX"
		443 X≠0?
		444 XEQ 34
		445 RCL "UMIN"
		446 X≠0?
		447 XEQ 34
		448 INDEX "VRMIN"
		449 3
		450 1
		451 STOIJ
		452 RCLEL
		453 INDEX "VDIR"
		454 3
		455 1
		456 STOIJ
		457 RCLEL
		458 X<>Y
		459 R↓
		460 X<>Y
		461 R↓
		462 ÷
		463 +/-
		464 XEQ 33
	465▸LBL 31
		466 RCL 11
		467 XEQ c
		468 XEQ 32
		469 RCL 12
		470 PIXEL
		471 RCL "XMAX"
		472 RCL- "XMIN"
		473 RCL "ResX"
		474 ÷
		475 STO+ 11
		476 ISG 12
		477 GTO 31
		478 PRLCD
		479 STOP
		480 MENU
		481 RTN
	482▸LBL 32
		483 RCL- "YMIN"
		484 RCL× 10
		485 RCL "ResY"
		486 -
		487 X>0?
		488 CLX
		489 ABS
		490 RTN
	491▸LBL 33
		492 RCL- "XMIN"
		493 RCL "XMAX"
		494 RCL- "XMIN"
		495 ÷
		496 RCL "ResX"
		497 ×
		498 +/-
		499 1
		500 X<>Y
		501 PIXEL
		502 RTN
	503▸LBL 34
		504 XEQ 32
		505 +/-
		506 1
		507 PIXEL
		508 RTN
	509▸LBL c
		510 RCL× "VDIR"
		511 RCL+ "VRMIN"
		512 STO "VR"
		513 XEQ "UFORT"
		514 1380649ᴇ-29
		515 ÷
		516 1ᴇ6
		517 ×
		518 RTN
519▸LBL 41
	520 166
	521 RCL "POLARI"
	522 X≠Y?
	523 SF 04
	524 R↓
	525 STO "POLARI"
	526 168
	527 STO "MASS"
	    "168Er"
	    ASTO "ATOM"
	528 "Set to 168Er"
	529 AVIEW
	530 RTN
531▸LBL 42
	532 162.5
	533 RCL "POLARI"
	534 X≠Y?
	535 SF 04
	536 R↓
	537 STO "POLARI"
	538 174
	539 STO "MASS"
	    "174Yb"
	    ASTO "ATOM"
	540 "Set to 174Yb"
	541 AVIEW
	542 RTN
543▸LBL 43
	544 269
	545 RCL "POLARI"
	546 X≠Y?
	547 SF 04
	548 R↓
	549 STO "POLARI"
	550 7
	551 STO "MASS"
	    "7Li"
	    ASTO "ATOM"
	552 "Set to 7Li"
	553 AVIEW
	554 RTN
LBL 51
	SF 12
	"FORT summary"
	PRA
	"============"
	PRA
	CF 12
	ADV
	"Species: "
	ARCL "ATOM"
	PRA
	"Polariz.: "
	ARCL "POLARI"
	├" a.u."
	PRA
	ADV
	"H-FORT U = "
	ARCL "H-VOLT"
	├" V"
	PRA
	"       P = "
	ARCL "PWRH"
	├" W"
	PRA
	"V-FORT U = "
	ARCL "PWRV"
	├" W"
	PRA
	ADV
	XEQ 06
	ADV
	"Trap "
	├"frequencies:"
	PRA
	INDEX "TRAPF"
	LBL 52
		"x "
		RCLEL
		1ᴇ3
		X>Y?
			├"  "
		R↓
		100
		X>Y?
			├" "
		R↓
		10
		X>Y?
			├" "
		R↓
		ARCL ST X
		├" Hz"
		PRA
		J+
		FC? 77
			GTO 52
	ADV
	"→ "
	DATE
	ADATE
	├" "
	TIME
	ATIME24
	├" ←"
	PRA
	ADV
	ADV
	ADV	
	RTN
555▸LBL 99
	556 CF 04
	557 CLMENU
	558 EXITALL
	559 END
